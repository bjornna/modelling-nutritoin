archetype (adl_version=1.4; uid=db616764-b35e-42cd-bc97-5e313479eaf5)
	openEHR-EHR-OBSERVATION.nutrition_intake.v0

concept
	[at0000]

language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
			>
		>
	>

description
	original_author = <
		["date"] = <"2018-11-29">
		["name"] = <"Silje Ljosland Bakke">
		["organisation"] = <"Nasjonal IKT HF">
		["email"] = <"silje.ljosland.bakke@nasjonalikt.no">
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Silje Ljosland Bakke, Nasjonal IKT HF, Norway (openEHR Editor)","Heather Leslie, Atomica Informatics, Australia (openEHR Editor)","Terje Sagmyr, DIPS AS, Norway">
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For registrering av det totale inntaket av næringsstoffer på et tidspunkt eller tidsintervall, for eksempel et måltid eller en dag.">
			keywords = <"ernæring","næringsstoffer">
			copyright = <"© openEHR Foundation">
			use = <"Brukes til å registrere det totale inntaket av næringsstoffer på et tidspunkt eller tidsintervall, for eksempel et måltid eller en dag.">
			misuse = <"Skal ikke brukes til å registrere detaljene om matvarer som konsumeres. Bruk arketypen OBSERVATION.food_item til dette formålet.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording the total intake of nutrients at a point in time or interval of time, such as a meal or a day.">
			keywords = <"nutrition","nutrients">
			copyright = <"© openEHR Foundation">
			use = <"Use for recording the total intake of nutrients at a point in time or interval of time, such as a meal or a day.">
			misuse = <"Not to be used for recording the details of food items consumed. Use the OBSERVATION.food_item archetype for this purpose.">
		>
	>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"b45b1c45da832ac333a9c1bbbf121c29">
		["build_uid"] = <"35a112a2-5b2c-48f6-b4a0-4b60f5502d28">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Nutrition intake
		data matches {
			HISTORY[at0001] matches {    -- History
				events cardinality matches {0..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {    -- Any Event
						data matches {
							ITEM_TREE[at0003] matches {    -- ItemTree
								items cardinality matches {0..*; unordered} matches {
									allow_archetype CLUSTER[at0010] occurrences matches {0..*} matches {    -- Nutrients
										include
											archetype_id/value matches {/openEHR-EHR-CLUSTER\.dietary_nutrients(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.dietary_nutrients(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.macronutrients(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.macronutrients(-[a-zA-Z0-9_]+)*\.v1|openEHR-EHR-CLUSTER\.micronutrients(-[a-zA-Z0-9_]+)*\.v0|openEHR-EHR-CLUSTER\.micronutrients(-[a-zA-Z0-9_]+)*\.v1/}
									}
									ELEMENT[at0011] occurrences matches {0..1} matches {    -- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
					EVENT[at0015] occurrences matches {0..*} matches {    -- Meal
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/data[at0003]
						}
					}
					INTERVAL_EVENT[at0004] occurrences matches {0..*} matches {    -- Cumulative total
						data matches {
							use_node ITEM_TREE /data[at0001]/events[at0002]/data[at0003]
						}
						math_function matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[openehr::148]
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0012] matches {    -- ItemTree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0013] occurrences matches {0..1} matches {    -- Nutritional day definition
						value matches {
							DV_TIME matches {*}
						}
					}
					allow_archetype CLUSTER[at0014] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Nutrition intake">
					description = <"The total intake of nutrients at a point in time or interval of time, such as a meal or a day.">
				>
				["at0001"] = <
					text = <"History">
					description = <"*">
				>
				["at0002"] = <
					text = <"Any Event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Cumulative total">
					description = <"Cumulative nutritional intake within a specified interval of time, which should be explicitly defined in a template or at run-time.">
					comment = <"A common use case is to record the cumulative total nutritional intake from the start time of a \"nutritional day\" until the time of recording. The start time of the \"nutritional day\" will be the start time for this event – for example 06:00. It may be defined by the application or by the value recorded in the 'Nutritional day definition' element in the Protocol of this archetype.">
				>
				["at0010"] = <
					text = <"Nutrients">
					description = <"Details about the component nutrients for the event.">
				>
				["at0011"] = <
					text = <"Comment">
					description = <"Additional narrative about the nutrition intake not captured in other fields.">
				>
				["at0012"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0013"] = <
					text = <"Nutritional day definition">
					description = <"The applied definition for the starting time of the 'Cumulative total' event used in this archetype.">
					comment = <"For example: 00:00 or 06:00.">
				>
				["at0014"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: Local hospital departmental infomation or additional metadata to align with FHIR or CIMI equivalents.">
				>
				["at0015"] = <
					text = <"Meal">
					description = <"A unspecified meal, either as a point in time or interval event, which should be explicitly defined and renamed in a template or at run-time.">
					comment = <"For example: Breakfast or lunch.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Næringsinntak">
					description = <"Det totale inntaket av næringsstoffer på et tidspunkt eller tidsintervall, for eksempel et måltid eller en dag.">
				>
				["at0001"] = <
					text = <"Historie">
					description = <"*">
				>
				["at0002"] = <
					text = <"Enhver hendelse">
					description = <"Standard, uspesifisert tidspunkt eller intervallhendelse som kan være eksplisitt definert i en mal eller ved kjøring.">
				>
				["at0003"] = <
					text = <"ItemTree">
					description = <"@ intern @">
				>
				["at0004"] = <
					text = <"Akkumulert total">
					description = <"Kumulativt næringsinntak innen et spesifisert tidsintervall, som skal være eksplisitt definert i en mal eller ved kjøring.">
					comment = <"En vanlig brukssak er å registrere det kumulative totale næringsinntaket fra starttidspunktet for en \"ernæringsdag\" til registreringstidspunktet. Starttidspunktet for \"ernæringsdagen\" vil være starttidspunktet for dette arrangementet – for eksempel 06:00. Det kan defineres av applikasjonen eller av verdien som er registrert i elementet 'Ernæringsdagsdefinisjon' i protokollen til denne arketypen.">
				>
				["at0010"] = <
					text = <"Næringsstoffer">
					description = <"Detaljer om komponenten næringsstoffer for arrangementet.">
				>
				["at0011"] = <
					text = <"Kommentar">
					description = <"Ytterligere fortelling om næringsinntaket som ikke fanges opp i andre felt.">
				>
				["at0012"] = <
					text = <"ItemTree">
					description = <"@ intern @">
				>
				["at0013"] = <
					text = <"Definisjon av ernæringsdag">
					description = <"Den anvendte definisjonen for starttidspunktet for hendelsen «Kumulert total» som brukes i denne arketypen.">
					comment = <"For eksempel: 00:00 eller 06:00.">
				>
				["at0014"] = <
					text = <"Utvidelse">
					description = <"Ytterligere informasjon som kreves for å fange opp lokalt innhold eller for å tilpasse seg andre referansemodeller/formalismer.">
					comment = <"For eksempel: Informasjon om lokale sykehusavdelinger eller tilleggsmetadata for å samsvare med FHIR- eller CIMI-ekvivalenter.">
				>
				["at0015"] = <
					text = <"Måltid">
					description = <"Et uspesifisert måltid, enten som et tidspunkt eller intervallhendelse, som skal være eksplisitt definert og omdøpt i en mal eller under kjøring.">
					comment = <"For eksempel: Frokost eller lunsj.">
				>
			>
		>
	>
